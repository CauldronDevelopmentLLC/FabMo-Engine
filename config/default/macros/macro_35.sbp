'!FABMO!name:HOME
'!FABMO!description:Bender: HOME the bend axis
' HOMING ROUTINE
' RYAN STURMER
' WRITTEN FOR FABMO PLATFORM FOR A DIWIRE

&approach_speed_fast = 100.0
&approach_speed_slow = 15.0
&search_distance_x = 500.0
&dist_backoff_x = -10.0
&dist_offset_x = -85.0

'Save the XY speed
&saved_speed = %(71)
&saved_ramp = %(81)

' Make sure we're off the switches
IF %(51) = 1 THEN GOSUB backoff_x
JS, 40 ' temp pull-back speed

' Get First Homing Touch in X
    VR, 30000
    VS, &approach_speed_fast
    ON INPUT(1,1) GOTO CONTINUE_fast
    MX, &search_distance_x
    GOTO FAIL
    CONTINUE_fast:
        GOSUB backoff_x

' Get More Reliable Touch
    VS, &approach_speed_slow
    ON INPUT(1,1) GOTO CONTINUE_slow
    MX, &search_distance_x
    GOTO FAIL
    CONTINUE_slow:
        VR,&saved_ramp
        GOSUB backoff_x

' Set Location and Zero
    GOSUB do_offset
    ZX

' Restore the XY speed
'VR, 100
VR,&saved_ramp
VS,&saved_speed
END

' SUBROUTINES BELOW HERE ---------------------------

FAIL:
    VR,&saved_ramp
    ' Couldn't find switch
    END

backoff_x:
    JX,%(1) + &dist_backoff_x
    RETURN
   
do_offset:
    JX,%(1) + &dist_offset_x
    RETURN
