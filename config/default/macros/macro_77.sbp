'!FABMO!name:Zero Plate Height
'!FABMO!description:HANDIBOT: Remeasure Height of Z-Zero Plate
'This routine allows you to determine the height of Z-Zero touch-off plate compared to material surface

$zero_plate_adder := .75
&temp_top = $current_cutter_Zoffset - 0.5
&temp_Zplate = $zero_plate_adder

'First Set XYZ Zero with routine if you have not (Macro#3). And, measure cutter length if you nave not (Macro#2).
	PAUSE

'The other thing you need to do is to carefully move the cutter tip exactly to the surface, and click the zero button there ... click QUIT now and do that if you have not.
	PAUSE

'OK, now we're ready to continue ...
	MZ, &temp_top
	M2, 6, 0

'Tool should be in position, Pull out Zeroing Plate and Put Clip on Shank of Cutter.
	PAUSE
		
'Get Values at Startup
		&startZ_speed = %(73)
		&startZ_ramp = %(83)

'Starting Settings for Measuring Z
        VS, 4, .5        
        &Z_plunge_target = -5
        &Z_pullup = .5
        &Z_final = 1.5

'HERE WE GO ! .............................................................
    'Next command creates an interrupt handler for contact with zero plate
        VS,,0.5
        VR,, 10000  ' quick stop
        ON INPUT(1,1) GOTO afterHit
        MZ, &Z_plunge_target
            GOTO FAIL
        afterHit:
            GOSUB Backoff

    'Now, let's do again with a touch at a reliable distance and speed
        VS,,.25
        VR,, 10000  ' quick stop
        ON INPUT(1,1) GOTO Finish
        MZ, &Z_plunge_target
            GOTO FAIL
        Finish:
            GOSUB Backoff

    'Set Location and Zero
		$zero_plate_adder = %(3) - &z_pullup
        $current_cutter_Zoffset = &temp_Zplate + $zero_plate_adder - &Z_pullup  ' remember we're pulled up here
        &new_base_Z_loc = &Z_pullup + $zero_plate_adder
        VA,,, &new_base_Z_loc,,,,,, &new_base_Z_loc

    'Restore Values
        VS, &startXY_speed, &startZ_speed
        VR, , &startZ_ramp 
        MZ, &Z_final        

    'Re-Measurement Complete. REMOVE Aligator Clip and PARK Target !! (-Continue- when Done)
        PAUSE

    '{{Eventually we'd like a choice here to move home or to park}}
        
END

'SUBROUTINES BELOW HERE ---------------------------

Backoff:
    &NextPullUp = %(3)
    &NextPullUp = &NextPullUp + &Z_pullup
    MZ, &NextPullUp
    RETURN

FAIL:
    'No Contact with Plate ... Ending!
    PAUSE
    END

Already_Triggered:
    'Z-contact seems already triggered ... Ending!
    PAUSE
    END
   
